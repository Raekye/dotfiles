" === TODO
" - cscope don't jump to first result when using quickfix.
" - shortcut to close quickfix/location list?
" - nerdtree split shenanigans...
" - how to do git diff --staged with fugitive?
" - open help in vertical split by default?

" === Notes
" - The file-local mark `i` is used to store the last position every time before exiting insert mode.

" === Tips/Reminders
" - <C-y> to accept current match in popup wildmenu.
"   No equivalent without popup menu (vim < 900)...

" === Global Options
" :help option-list
" To lookup a specific option, surround it in single quotes (e.g. `:help 'some-option'`).

set exrc " Load .vimrc in current directory (if it exists).
set secure " Disallow `:autocmd`, shell, and write commands in scripts loaded by setting `exrc`.

set number
set ruler
set scrolloff=4

set hidden
set backspace=indent,eol,start
set autoread
set noswapfile

" :help backup
set writebackup " Should be default.
set nobackup " Should be default.

set encoding=utf-8
set fileformats=unix,dos

set conceallevel=0
set list
set listchars=tab:>\ , " Add a comma here so that the space doesn't get trimmed by fixers/formatters.
set listchars+=eol:$
set listchars+=multispace:-,lead:-,trail:-
set listchars+=extends:>,precedes:<,conceal:!,nbsp:%
if v:version >= 900
	set listchars+=leadmultispace:\|---
else
endif

" This setting is the last "common configuration" under :help tabstop.
set tabstop=4
set shiftwidth=4
set noexpandtab

set autoindent
set smartindent " Less strict than cindent.
set breakindent " Wrapped lines continue indented.

set ignorecase
set smartcase
" Use <C-g> and <C-t> to move to next and previous match.
" :help c_CTRL-G
" :help c_CTRL-T
set incsearch
set hlsearch

set showcmd
set laststatus=2 " Always show status line for last window.

set wildmenu " Command line completion.
if v:version >= 900
	set wildoptions=pum " Display using popup menu.
endif

set pastetoggle=<F2>

set splitright

set cursorline
set cursorlineopt=number

set sessionoptions=buffers

if has('termguicolors')
	set termguicolors
endif

if v:version < 900
	" TODO what happened...
	set noesckeys
endif

" === cscope
" :help cscope
set cscopetag
set cscopequickfix=s-,g-,c- " Use quickfix for symbols, definitions, and callers.
autocmd QuickFixCmdPost cscope copen
" https://www.gnu.org/software/global/globaldoc_toc.html#Gtags_002dcscope
if executable('gtags-cscope')
	set cscopeprg=gtags-cscope
	if filereadable('GTAGS')
		cscope add GTAGS
	endif
endif

" === Configuration Variables
" :help :map-special-chars
let g:mapleader = ','
let g:maplocalleader = ','

" :help xterm-true-color
let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"

" :help filetype
let g:markdown_recommended_style = 0 " Don't expandtab.
let g:python_recommended_style = 0 " Don't expandtab.

" :help rust-settings
let g:rust_recommended_style = 0 " Don't expandtab.
let g:rustfmt_autosave = 1
let g:rustfmt_fail_silently = 1 " Don't populate location list.

" :help vim-markdown-options
let g:vim_markdown_folding_disabled = 1
let g:vim_markdown_math = 1
let g:vim_markdown_frontmatter = 1
let g:vim_markdown_auto_insert_bullets = 0
let g:vim_markdown_new_list_item_indent = 0

" :help NERDTreeSettings
let g:NERDTreeShowLineNumbers = 1
let g:NERDTreeShowHidden = 1
"let g:NERDTreeHijackNetrw = 1

" :help NERDCommenterOptions
let g:NERDCreateDefaultMappings = 0

" :help airline-configuration
let g:airline_theme = 'base16'
let g:airline_symbols_ascii = 1

" :help ale-contents
set omnifunc=ale#completion#OmniFunc
let g:ale_completion_enabled = 1
let g:ale_linters = {
	\ 'c': ['clangd'],
	\ 'cpp': ['clangd'],
	\ 'rust': ['analyzer'],
	\}
let g:ale_fixers = {
	\'*': ['remove_trailing_lines', 'trim_whitespace'],
	\}
let g:ale_fix_on_save = 1
"let g:ale_virtualtext_cursor = 0

" :help GundoConfig
let g:gundo_prefer_python3 = 1

" === Old
if executable('ag')
	let g:ackprg = 'ag --vimgrep'
else
	let g:ackprg = 'grep --recursive --line-number'
endif

" === https://github.com/junegunn/fzf.vim
function s:rg_files(dir, fullscreen)
	call fzf#run(fzf#wrap(fzf#vim#with_preview({ 'source': 'rg --files -- ' .. a:dir }), a:fullscreen))
endfunction

" Don't escape `<q-args>` so that the dir (for `grep`/`rg`) can be specified.
command -bang -nargs=* -complete=dir Grep call fzf#vim#grep('grep --recursive --line-number --extended-regexp -- ' .. <q-args>, fzf#vim#with_preview(), <bang>0)
command -bang -nargs=* -complete=dir Rg call fzf#vim#grep('rg --column --line-number --no-heading --color=always --smart-case -- ' .. <q-args>, fzf#vim#with_preview(), <bang>0)
command -bang -nargs=* -complete=dir RgFiles call s:rg_files(<q-args>, <bang>0)

" === https://github.com/junegunn/vim-plug
call plug#begin('~/.vim/plugged')

Plug 'lifepillar/vim-solarized8'

Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'

Plug 'preservim/nerdtree'
Plug 'preservim/nerdcommenter'
Plug 'preservim/tagbar'

Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
Plug 'junegunn/fzf.vim'
"Plug 'mileszs/ack.vim'

Plug 'tpope/vim-surround'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-rails'
" Enhance 'ga' output (shows info about character under the cursor).
Plug 'tpope/vim-characterize'
"Plug 'tpope/vim-vinegar'
Plug 'tpope/vim-obsession'
Plug 'tpope/vim-sleuth'

Plug 'easymotion/vim-easymotion'

Plug 'dense-analysis/ale'

" https://github.com/junegunn/vim-plug/issues/1005
Plug 'airblade/vim-gitgutter', { 'branch': 'main' }

" The `rust.vim`/`rustfmt.vim` distributed with vim on Fedora 38 is out of date and calls rustfmt incorrectly.
Plug 'rust-lang/rust.vim'

Plug 'https://github.com/preservim/vim-markdown'

" Not really used...
Plug 'sjl/gundo.vim'

" Automatically executes `filetype plugin indent on` and `syntax on`.
call plug#end()

set background=light

" :help solarized8-options
let g:solarized_visibility = "low"
if isdirectory($HOME .. '/.vim/plugged/vim-solarized8')
	" The "high" here corresponds to `g:solarized_contrast`.
	colorscheme solarized8_high
endif

" === Shortcuts
" :help index
" :help :map-modes
" :help :map-commands
" - `:map` -> `:no[remap]`: `mapmode-nvo`.
" - `:nmap` -> `:nn[oremap]`: `mapmode-n`.
" - `:xmap` -> `:xn[oremap]`: `mapmode-x` (visual mode only; `mapmode-v` includes select mode).
" - `:cmap` -> `:cn[oremap]`: `mapmode-c`.

" :help mode-Ex
nn Q <Nop>
" :help CTRL-6
nn <C-j> <C-^>

nn <C-l> <C-l>:nohlsearch<CR>

nn <silent> <C-c> :cclose<CR>:lclose<CR>:helpclose<CR>:NERDTreeClose<CR>

nn <C-n> :NERDTreeToggle<CR>
nn <C-k> :Buffers<CR>
nn <C-g> :TagbarToggle<CR>

nn <C-h> <Plug>NERDCommenterToggle
xn <C-h> <Plug>NERDCommenterToggle

if executable('rg')
	nn <C-p> :RgFiles<CR>
	nn <leader>g :Rg<space>
else
	nn <C-p> :Files<CR>
	nn <leader>g :Grep<space>
endif

nn <leader>l :Lines<space>

nn <leader>w :ALESymbolSearch <C-R>=expand("<cword>")<CR><CR>
nn <leader>e <Plug>(ale_go_to_definition)
nn <leader>r <Plug>(ale_find_references)
nn [e <Plug>(ale_previous_wrap)
nn ]e <Plug>(ale_next_wrap)

" Find symbols, definitions, callers.
nn <leader>s :cscope find s<space>
nn <leader>d :cscope find g<space>
nn <leader>f :cscope find c<space>

nn <leader>v :vert Git<CR>

nn <leader>u :GundoToggle<CR>

" By default, `<F1>` immediately opens help.
" Remap it to prompt for a topic.
nn <F1> :vert help<space>
" Disable `<F1>` in `mapmode-c` so it can be mashed.
cno <F1> <Nop>

" https://github.com/mhinz/vim-galore#saner-behavior-of-n-and-n
" Make 'n' and 'N' search direction agnostic of '/' and '?'.
nn <expr> n 'Nn'[v:searchforward]
nn <expr> N 'nN'[v:searchforward]

" https://github.com/mhinz/vim-galore#saner-command-line-history
" Make '<C-n>' and '<C-p>' autocomplete based on matching prefix.
cno <expr> <C-n> wildmenumode() ? "\<C-n>" : "\<Down>"
cno <expr> <C-p> wildmenumode() ? "\<C-p>" : "\<Up>"

" === Autocommands
" :help autocmd-define
" :help autocmd-events

" Before exiting insert mode, create a mark and jump to it, to push it onto the jump list.
" :help mark
" :help jump-motions
autocmd InsertLeave * normal! mi `i
